import com.github.jengelman.gradle.plugins.shadow.ShadowPlugin
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'groovy'
    id 'project-report'
    id 'idea'
    id 'java-gradle-plugin'
    id 'signing'
    id 'com.gradle.plugin-publish' version '1.2.1'
    id 'org.ajoberstar.git-publish' version '4.2.0'
    id 'com.github.node-gradle.node' version '5.0.0'
}

apply plugin: ShadowPlugin

apply from: file('gradle/docs.gradle')
apply from: file('gradle/publish.gradle')
apply from: file('gradle/vuepress.gradle')
apply from: file('gradle/ghPages.gradle')
apply from: file('gradle/dependencies.gradle')

repositories {
    gradlePluginPortal()
    mavenCentral()
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    if (System.env.CI == 'true') {
        testLogging.showStandardStreams = true
        minHeapSize "1g"
        maxHeapSize "1g"
    }

    systemProperty 'java.io.tmpdir', project.layout.buildDirectory.asFile.get().absolutePath

    // Required to test configuration cache in tests when using withDebug()
    // https://github.com/gradle/gradle/issues/22765#issuecomment-1339427241
    jvmArgs(
            "--add-opens",
            "java.base/java.util=ALL-UNNAMED",
            "--add-opens",
            "java.base/java.util.concurrent.atomic=ALL-UNNAMED",
            "--add-opens",
            "java.base/java.lang.invoke=ALL-UNNAMED",
            "--add-opens",
            "java.base/java.net=ALL-UNNAMED",
    )
}

// Remove the gradleApi so it isn't merged into the jar file.
// This is required because 'java-gradle-plugin' adds gradleApi() to the 'api' configuration.
// See https://github.com/gradle/gradle/blob/master/subprojects/plugin-development/src/main/java/org/gradle/plugin/devel/plugins/JavaGradlePluginPlugin.java#L162
configurations.named(JavaPlugin.API_CONFIGURATION_NAME) {
    dependencies.remove(project.dependencies.gradleApi())
}

tasks.named('shadowJar', ShadowJar) {
    from rootProject.file('LICENSE')
    from rootProject.file('NOTICE')
    enableRelocation true
}

idea {
    project {
        languageLevel = '1.8'
    }
}

tasks.named('ideaModule') {
    notCompatibleWithConfigurationCache("https://github.com/gradle/gradle/issues/13480")
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

tasks.register('downloadDependencies', Exec) {
    dependsOn configurations.testRuntimeClasspath
    commandLine 'echo', 'Downloaded all dependencies'
}
