pluginManagement {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id 'com.gradle.develocity' version '3.17.2'
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
    }
}

def isCI = providers.environmentVariable('CI').present

develocity {
    buildScan {
        termsOfUseUrl = 'https://gradle.com/terms-of-service'
        termsOfUseAgree = 'yes'
        publishing.onlyIf { isCI }
        if (isCI) {
            tag 'CI'
            if (System.env.CIRCLE_TAG) {
                link 'VCS', "https://github.com/johnrengelman/shadow/tree/${System.env.CIRCLE_TAG}"
            } else {
                link 'VCS', "https://github.com/johnrengelman/shadow/tree/${System.env.CIRCLE_BRANCH}"
            }
            link 'VCS Commit', "https://github.com/johnrengelman/shadow/commit/${System.env.CIRCLE_SHA1}"
            if (System.env.CI_PULL_REQUEST) {
                link 'Pull Request', "${System.env.CI_PULL_REQUEST}"
            }
        }
    }
}

enableFeaturePreview("STABLE_CONFIGURATION_CACHE")

rootProject.name = 'shadow'