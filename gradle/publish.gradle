apply plugin: "com.gradle.plugin-publish"
apply plugin: "com.vanniktech.maven.publish"

group = providers.gradleProperty("GROUP").get()
version = providers.gradleProperty("VERSION_NAME").get()

gradlePlugin {
    website = providers.gradleProperty("POM_URL")
    vcsUrl = providers.gradleProperty("POM_URL")

    plugins {
        shadowPlugin {
            id = 'com.gradleup.shadow'
            implementationClass = 'com.github.jengelman.gradle.plugins.shadow.ShadowPlugin'
            displayName = providers.gradleProperty("POM_NAME").get()
            description = providers.gradleProperty("POM_DESCRIPTION").get()
            tags = ['onejar', 'shade', 'fatjar', 'uberjar']
        }
    }
}

tasks.named('publishPlugins') {
    doFirst {
        if (version.endsWith("SNAPSHOT")) {
            throw new GradleException('Cannot publish SNAPSHOT versions to Plugin Portal!')
        }
    }
    notCompatibleWithConfigurationCache("https://github.com/gradle/gradle/issues/21283")
}

tasks.register('release') {
    dependsOn tasks.named('publish'), tasks.named('publishPlugins'), tasks.named('gitPublishPush')
}
